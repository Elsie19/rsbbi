// <Book> <Section>{:<Verse>{-<Range>}}
total = { SOI ~ set ~ EOI }

WHITESPACE = _{ " " }

whitespace_with_commas = _{ " " | ", " | "\t" | "_" }

set = _{
    book ~ ("." | "_")? ~ identifier
}

book = ${
    // Conforms to everything in https://developers.sefaria.org/docs/text-references#text-references-aka-citations
    (number ~ WHITESPACE)? ~ ((ASCII_ALPHA+ ~ (whitespace_with_commas ~ ASCII_ALPHA+)*)? ~ ("."? ~ "")?)
}

// This can be either something like `chapter:verse`, `chapter:verse-verse`, or `chapter:verse-chapter:verse`
identifier = _{
    (chapter_range | (section ~ ((":" | "." | ",") ~ verse)?)?)
}

section = {
    ASCII_ALPHANUMERIC+
}

verse = {
    range
  | verse_number
}

// Number can be any number, as long as it does not *start* with 0
number = _{ ASCII_NONZERO_DIGIT ~ (ASCII_DIGIT+)? }

verse_number = { ASCII_DIGIT+ }

// 1: 1-3
// 2: 1:1-4:3
range         = { ASCII_ALPHANUMERIC+ ~ "-" ~ ASCII_ALPHANUMERIC+ }
chapter_range = { (#first_section = section ~ (":" | "." | ",") ~ #first_verse = verse_number) ~ "-" ~ (#second_section = section ~ (":" | "." | ",") ~ #second_verse = verse_number) }
